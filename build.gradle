buildscript {
    ext.kotlin_version = '1.4.32'
    repositories {
        google()
        jcenter()
        maven {
            url "http://storage.googleapis.com/r8-releases/raw/master"
        }
        maven {
            url 'https://kotlin.bintray.com/kotlinx/'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {

        // Gradle
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // CI Scanning & Retry
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
        classpath 'org.ow2.asm:asm:9.0'
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.16.0"
        classpath "org.gradle:test-retry-gradle-plugin:1.2.0"
        classpath "com.autonomousapps:dependency-analysis-gradle-plugin:0.72.0"

        // Firebase
        classpath "com.google.gms:google-services:4.3.5"
        classpath "com.google.firebase:perf-plugin:1.3.5"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.5.2'

        // Dependency Publishing
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.10.0"

        // Realm Database
        classpath "io.realm:realm-gradle-plugin:10.4.0"

        // Android X Navigation components
        def androidx_navigation_version = "2.3.0-alpha04"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$androidx_navigation_version"

        // Deployment
        classpath "com.github.triplet.gradle:play-publisher:3.4.0"
        classpath "com.google.firebase:firebase-appdistribution-gradle:2.1.1"
    }

}

apply from: "buildSrc${File.separator}dependencies.gradle"
apply from: "ci${File.separator}scanning${File.separator}sonarqube.gradle"
apply from: "ci${File.separator}scanning${File.separator}jacoco.gradle"
apply from: "ci${File.separator}scanning${File.separator}dependency_health.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }

    // Required for deps that use Java 8 (okhttp)
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runAllJacocoTests(type: GradleBuild) {
    group = "verification"
    tasks = ['clientapi:jacocoTestReportDebug', 'core:jacocoTestReportDebug',
             'id:jacocoTestReportDebug', 'face:jacocoTestReportDebug',
             'fingerprint:jacocoTestReportDebug', 'fingerprintscanner:jacocoTestReportDebug']
}

/*
Run tests in parallel to speed up tests
https://docs.gradle.org/nightly/userguide/performance.html#suggestions_for_java_projects
*/

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
