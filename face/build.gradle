apply plugin: 'com.android.dynamic-feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

sonarqube {
    properties {
        property "sonar.sources", "src/main/java"
    }
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion config.min_sdk
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        staging {
            initWith release
        }

        debug {
            testCoverageEnabled true
        }
    }

    flavorDimensions "mode"

    productFlavors {
        standard {
            dimension "mode"
        }

        withLogFile {
            dimension "mode"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven {
        url "http://178.62.61.39:8081/artifactory/libs-release-local"
        credentials {
            username = "${SIMPRINTS_ARTIFACTORY_USERNAME}"
            password = "${SIMPRINTS_ARTIFACTORY_PASSWORD}"
        }
    }
}

dependencies {
    // https://issuetracker.google.com/issues/132906456
    // When Unit tests are launched in CL, the classes.jar for the base module is not included in the final testing classes.jar file.
    // So the tests that have references to the base module fail with java.lang.NoClassDefFoundError exceptions.
    // The following line includes the base module classes.jar into the final one.
    // To run unit tests from CL: ./gradlew fingerprint:test
    testRuntimeOnly fileTree(include: ['**/*.jar'], dir: '../id/build/intermediates/app_classes/')
    
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':id')
    implementation project(':uicomponents')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation deps.cameraView
    implementation deps.circleImageView

    // Retrofit
    implementation deps.retrofit.core
    implementation deps.retrofit.logging
    implementation deps.retrofit.okhttp

    // Android X
    androidTestImplementation deps.testing.androidx.core_testing
    androidTestImplementation deps.testing.androidx.monitor
    androidTestImplementation deps.testing.androidx.core
    androidTestImplementation deps.testing.androidx.ext_junit
    androidTestImplementation deps.testing.androidx.runner
    androidTestImplementation deps.testing.androidx.rules
    androidTestUtil deps.testing.androidx.orchestrator

    androidTestImplementation deps.testing.mockk.android
    androidTestImplementation deps.testing.truth

    // Espresso
    androidTestImplementation deps.testing.espresso.core
    androidTestImplementation deps.testing.espresso.intents
    androidTestImplementation(deps.testing.espresso.barista) {
        exclude group: "com.android.support"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.jetbrains.kotlin"
        exclude group: "com.google.guava"
    }

    // Koin
    androidTestImplementation deps.koin.core_ext
    androidTestImplementation deps.testing.koin

    // ######################################################
    //                      Unit test
    // ######################################################

    // Simprints
    testImplementation project(':testtools')
    testImplementation(deps.testing.junit) {
        exclude group: "com.android.support"
    }

    // Android X
    testImplementation deps.testing.androidx.ext_junit
    testImplementation deps.testing.androidx.core
    testImplementation deps.testing.androidx.core_testing
    testImplementation deps.testing.androidx.runner

    // Kotlin
    testImplementation deps.testing.coroutines_test

    // Mockk
    testImplementation deps.testing.mockk.core
    testImplementation deps.testing.truth
    testImplementation deps.testing.robolectric.core
}
