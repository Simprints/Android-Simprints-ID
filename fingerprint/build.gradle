apply plugin: 'com.android.dynamic-feature'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion config.min_sdk
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "2019.3.0-dev0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            proguardFiles 'proguard-rules-dynamic-features.pro'
        }
        releaseWithLogfile {
            initWith release
        }
    }

    sourceSets {
        String sharedTestDir = 'src/commontesttools/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    // https://github.com/mockito/mockito/issues/1376
    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven {
        url "http://178.62.61.39:8081/artifactory/libs-release-local"
        credentials {
            username = "${SIMPRINTS_ARTIFACTORY_USERNAME}"
            password = "${SIMPRINTS_ARTIFACTORY_PASSWORD}"
        }
    }
}

dependencies {
    // https://issuetracker.google.com/issues/132906456
    // When Unit tests are launched in CL, the classes.jar for the base module is not included in the final testing classes.jar file.
    // So the tests that have references to the base module fail with java.lang.NoClassDefFoundError exceptions.
    // The following line includes the base module classes.jar into the final one.
    // To run unit tests from CL: ./gradlew fingerprint:test
    testRuntimeOnly(files("${projectDir}/../id/build/intermediates/app_classes/debug/bundleDebugClasses/classes.jar"))

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Simprints
    implementation project(':id')
    implementation project(':libsimprints')
    implementation project(':fingerprintmatcher')
    implementation project(':fingerprintscanner')
    implementation project(':fingerprintscannermock')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation deps.kotlin.reflect
    implementation deps.kotlin.anko

    // Android X
    implementation deps.androidx.core
    implementation deps.androidx.multidex
    implementation deps.androidx.appcompat
    implementation deps.androidx.legacy
    implementation deps.androidx.room.core
    implementation deps.androidx.lifecycle.viewmodel
    implementation deps.androidx.lifecycle.livedata
    implementation deps.androidx.ui.constraintlayout
    implementation deps.androidx.ui.cardview
    implementation deps.androidx.ui.preference
    implementation deps.androidx.ui.fragment
    implementation deps.arch_components.work

    // RxJava
    implementation deps.rxjava2.permissions
    implementation deps.rxjava2.location
    implementation deps.rxjava2.android
    implementation deps.rxjava2.core
    implementation deps.rxjava2.kotlin

    // Timber
    implementation deps.timber.core

    // Play Services
    implementation deps.playservices.location

    // ######################################################
    //                      Unit test
    // ######################################################

    // Simprints
    testImplementation project(':testtools')
    testImplementation(deps.testing.junit) {
        exclude group: "com.android.support"
    }

    // Android X
    testImplementation deps.testing.androidx.ext_junit
    testImplementation deps.testing.androidx.core
    testImplementation deps.testing.androidx.core_testing
    testImplementation deps.testing.androidx.runner
    testImplementation deps.testing.androidx.room
    testImplementation deps.testing.androidx.rules

    // Espresso
    testImplementation deps.testing.espresso.core
    testImplementation deps.testing.espresso.intents
    testImplementation deps.testing.espresso.contrib

    // Kotlin
    testImplementation deps.testing.kotlin
    testImplementation deps.testing.coroutines_test

    // Mocking and assertion frameworks
    testImplementation deps.testing.mockito.kotlin
    testImplementation deps.testing.truth
    testImplementation deps.testing.mockk.core

    // Koin
    testImplementation deps.testing.koin

    // Robolectric
    testImplementation deps.testing.robolectric.core
    testImplementation deps.testing.robolectric.multidex

    // ######################################################
    //                      Android test
    // ######################################################

    // Simprints
    androidTestImplementation(project(':testtools')) {
        exclude group: 'org.apache.maven'
        exclude group: 'org.mockito'
        exclude group: 'org.robolectric'
        exclude group: 'org.jetbrains.kotlinx'
        exclude group: 'io.mockk'
    }

    // Koin
    androidTestImplementation deps.koin.core_ext
    androidTestImplementation deps.testing.koin

    // Android X
    androidTestImplementation deps.testing.androidx.core_testing
    androidTestImplementation deps.testing.androidx.monitor
    androidTestImplementation deps.testing.androidx.core
    androidTestImplementation deps.testing.androidx.ext_junit
    androidTestImplementation deps.testing.androidx.runner
    androidTestImplementation deps.testing.androidx.rules
    androidTestUtil deps.testing.androidx.orchestrator
    androidTestImplementation deps.testing.live_data

    // Android X navigation components
    androidTestImplementation deps.testing.androidx.navigation

    // Mocking and assertion frameworks
    androidTestImplementation deps.testing.mockito.core
    androidTestImplementation deps.testing.mockito.android
    androidTestImplementation deps.testing.mockito.kotlin
    androidTestImplementation deps.testing.truth
    androidTestImplementation deps.testing.mockk.core
    androidTestImplementation deps.testing.mockk.android

    // Espresso
    androidTestImplementation deps.testing.espresso.core
    androidTestImplementation deps.testing.espresso.intents
    androidTestImplementation(deps.testing.espresso.barista) {
        exclude group: "com.android.support"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.jetbrains.kotlin"
        exclude group: "com.google.guava"
    }

    // Truth
    androidTestImplementation deps.testing.truth

    androidTestImplementation deps.testing.rx2_idler

    // Trust me I hate this fix more than you
    // This is to solve multiple imports of guava https://stackoverflow.com/a/60492942/4072335
    androidTestImplementation 'com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava'
}
