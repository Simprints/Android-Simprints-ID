apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

apply from: 'profiling.gradle'

project.version = "2019.1.0"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion config.min_sdk
        targetSdkVersion 28
        versionCode 2
        versionName project.version
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles.add(file('proguard-rules.pro'))
        }
        releaseWithLogfile {
            initWith release
        }
        profiling {
            initWith debug
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':libsimprints')

    implementation deps.androidx.appcompat

    testImplementation 'junit:junit:4.12'
}

def libraryGroupId = 'com.simprints'
def libraryArtifactId = 'fingerprintmatcher'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            //noinspection GroovyAssignabilityCheck
            version project.version
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
        }
    }
}

artifactory {
    contextUrl = 'http://178.62.61.39:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = "${SIMPRINTS_ARTIFACTORY_USERNAME}"
            password = "${SIMPRINTS_ARTIFACTORY_PASSWORD}"
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
