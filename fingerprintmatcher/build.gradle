apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: 'kotlin-android-extensions'

apply from: 'profiling.gradle'
apply from: "${rootDir}${File.separator}buildSrc${File.separator}build_config.gradle"

android {
    compileSdkVersion 29
    defaultConfig {
        targetSdkVersion 29
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles.add(file('proguard-rules.pro'))
        }
        profiling {
            initWith debug
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation deps.androidx.appcompat

    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines_core

    testImplementation 'junit:junit:4.12'
}

def libraryGroupId = 'com.simprints'
def libraryArtifactId = 'fingerprintmatcher'

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            //noinspection GroovyAssignabilityCheck
            version project.version
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
        }
    }
}
