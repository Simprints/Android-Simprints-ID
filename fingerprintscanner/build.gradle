apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: "org.gradle.test-retry"

apply from: "${rootDir}${File.separator}buildSrc${File.separator}build_properties.gradle"

android {
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        staging {
            initWith release
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events 'started', 'passed', 'skipped', 'failed'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect

    // Android
    implementation deps.androidx.appcompat

    // Logging
    implementation deps.timber.core

    // RxJava
    implementation deps.rxjava2.core
    implementation deps.rxjava2.android
    implementation deps.rxjava2.kotlin

    // ######################################################
    //                      Unit test
    // ######################################################

    // Simprints
    testImplementation project(':testtools')

    // JUnit
    testImplementation deps.testing.junit

    // Mockito
    testImplementation deps.testing.mockito.kotlin
    testImplementation deps.testing.mockito.core
    testImplementation deps.testing.mockito.inline

    // Truth
    testImplementation deps.testing.truth
}

def libraryGroupId = 'com.simprints'
def libraryArtifactId = 'fingerprintscanner'

publishing.publications {
    aar(MavenPublication) {
        groupId libraryGroupId
        //noinspection GroovyAssignabilityCheck
        version project.version
        artifactId libraryArtifactId

        artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
    }
}

repositories {
    mavenCentral()
}

task {
    test {
        retry {
            maxRetries = 5
        }
    }
}
