apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

project.version = "2019.4.0-dev6"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion config.min_sdk
        targetSdkVersion 28
        versionCode 1
        versionName project.version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releaseWithLogfile {
            initWith release
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect

    // Android
    implementation deps.androidx.appcompat

    // RxJava
    implementation deps.rxjava2.core
    implementation deps.rxjava2.android
    implementation deps.rxjava2.kotlin

    // ######################################################
    //                      Unit test
    // ######################################################

    // Simprints
    testImplementation project(':testtools')

    // JUnit
    testImplementation deps.testing.junit

    // Mockito
    testImplementation deps.testing.mockito.kotlin
    testImplementation deps.testing.mockito.core
    testImplementation deps.testing.mockito.inline

    // Truth
    testImplementation deps.testing.truth
}

def libraryGroupId = 'com.simprints'
def libraryArtifactId = 'fingerprintscanner'

publishing.publications {
    aar(MavenPublication) {
        groupId libraryGroupId
        //noinspection GroovyAssignabilityCheck
        version project.version
        artifactId libraryArtifactId

        artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
    }
}

artifactory {
    contextUrl = 'http://178.62.61.39:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = SIMPRINTS_ARTIFACTORY_USERNAME
            password = SIMPRINTS_ARTIFACTORY_PASSWORD
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

repositories {
    mavenCentral()
}
