import com.android.builder.testing.api.DeviceProvider

apply plugin: "com.android.application"
apply plugin: 'com.github.triplet.play'
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"
apply plugin: "com.google.firebase.firebase-crash"
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: "realm-android"

kotlin {
    experimental {
        coroutines "enable"
    }
}

kapt {
    generateStubs = true
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 120
        versionName "2018.4.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //testInstrumentationRunner "com.simprints.id.UiTestsRunner" //To use custom TestApplication

    }

    //Add those properties in ~/.gradle/gradle.properties
    signingConfigs {
        release {
            storeFile file(SIMPRINTSID_RELEASE_STORE_FILE)
            storePassword SIMPRINTSID_RELEASE_STORE_PASSWORD
            keyAlias SIMPRINTSID_RELEASE_KEY_ALIAS
            keyPassword SIMPRINTSID_RELEASE_KEY_PASSWORD
        }
    }

    bundle {
        language {
            enableSplit = false
        }
    }
    
    buildTypes {
        release {
            lintOptions {
                // TODO : Remove whenever possible
                // Check in the Translations Editor for crucial translations that may be missing
                disable "MissingTranslation"
                abortOnError true
                fatal 'StopShip'
                checkReleaseBuilds true
            }
            minifyEnabled true
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            manifestPlaceholders = [firebase_performance_logcat_enabled: false, firebase_analytics_collection_enabled: true]
        }
        releaseWithLogfile {
            initWith release
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            versionNameSuffix "-dev"
            manifestPlaceholders = [firebase_performance_logcat_enabled: true, firebase_analytics_collection_enabled: true]
        }
//        endToEndTesting {
//            initWith debug
//            versionNameSuffix "-test"
//            testCoverageEnabled false
//        }
    }
    
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release" ||
            variant.buildType.name == "releaseWithLogfile") {

            buildConfigField("String", "GCP_PROJECT", "\"simprints-152315\"")
            buildConfigField("String", "ANDROID_AUTH_API_KEY", "\"AIzaSyDdGdHCjfLY6z3zZSBrSHvjvNjh6K6Dhmw\"")
            buildConfigField("String", "END_POINT", "\"prod\"")
        } else if (variant.buildType.name == "debug") {
            buildConfigField("String", "GCP_PROJECT", "\"${getDevProjectId()}\"")
            buildConfigField("String", "ANDROID_AUTH_API_KEY", "\"AIzaSyCxPFr61OtOyAsVoXKSSSBo-C4Jk1QHc7I\"")
            buildConfigField("String", "END_POINT", "\"dev\"")
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "$variant_for_android_tests") {
            buildConfigField("String", "SCANNER", "\"${SIMPRINTSID_TEST_SCANNER}\"")
            buildConfigField("String", "WIFI_NETWORK", "\"${SIMPRINTSID_TEST_WIFI}\"")
            buildConfigField("String", "WIFI_PASSWORD", "\"${SIMPRINTSID_TEST_WIFI_PASSWORD}\"")
        }
    }

    testBuildType "debug"
    productFlavors {
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

//For Android tests, uninstall any previous apk
afterEvaluate {
    if (tasks.findByPath(assembleTaskForEndToEndTest()) != null) {
        tasks.findByPath(assembleTaskForEndToEndTest()).dependsOn uninstallAll
    }
}

def getDevProjectId() {
    if (project.hasProperty("development_gcp_project")) {
        return development_gcp_project
    } else {
        return "simprints-dev"
    }
}

def connectedTaskForEndToEndTest() {
    return "connected${variant_for_android_tests.capitalize()}AndroidTest"
}

def assembleTaskForEndToEndTest() {
    return "assemble${variant_for_android_tests.capitalize()}AndroidTest"
}

project.afterEvaluate {
    if (gradle.startParameter.taskNames.findAll {
        it.contains(connectedTaskForEndToEndTest())
    }.size() > 0) {
        project.(connectedTaskForEndToEndTest()).doFirst {
            def originalProvider = deviceProvider
            deviceProvider = [
                    getName       : { originalProvider.getName() },
                    init          : { originalProvider.init() },
                    terminate     : { originalProvider.terminate() },
                    getDevices    : { filterDevices(originalProvider.getDevices()) },
                    getTimeoutInMs: { originalProvider.getTimeoutInMs() },
                    isConfigured  : { originalProvider.isConfigured() },
                    getMaxThreads : { originalProvider.getMaxThreads() }
            ] as DeviceProvider
        }
    }
}

// Required to run instrumented tests (gradlew connectedAndroidTest) on a single device.
// By default Android runs on all device at the same time
def filterDevices(connectedDevices) {
    def DEVICES_PROPERTY = "devices"
    def devices = connectedDevices
    if (project.hasProperty(DEVICES_PROPERTY)) {
        def devicesSerials = project.getProperties().get(DEVICES_PROPERTY).split(/,/)
        devices = connectedDevices.findAll { devicesSerials.contains(it.serialNumber) }
        println "[testy] ${!devices.empty ? "Execute on: ${devices.collect { it.serialNumber }}" : "Devices not found"}"
    }

    devices
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://maven.fabric.io/public" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven {
        url "http://178.62.61.39:8081/artifactory/libs-release-local"
        credentials {
            username = "${SIMPRINTS_ARTIFACTORY_USERNAME}"
            password = "${SIMPRINTS_ARTIFACTORY_PASSWORD}"
        }
    }
}

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}
// ./gradlew publishApkRelease to publish on internal channel
play {
    track = 'internal'
    jsonFile = file(SIMPRINTSID_PUBLISH_KEY_FILE)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':libcommon')
    implementation project(':libsimprints')

    // Dagger
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    compileOnly "org.glassfish:javax.annotation:10.0-b28"

    // Kotlin awesomeness
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    //implementation 'androidx.core:core-ktx:1.0.0-alpha1'

    // Support libraries
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // WorkManager
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    // Google
    implementation "com.google.android.gms:play-services-location:$google_version"
    implementation 'com.google.android.gms:play-services-places:15.0.1'
    implementation "com.google.android.gms:play-services-safetynet:$google_version"

    // Databases
    implementation "com.google.firebase:firebase-database:16.0.1"
    implementation "com.google.firebase:firebase-firestore:17.0.3"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.1"
    implementation "com.google.firebase:firebase-auth:16.0.2"
    implementation "com.google.firebase:firebase-config:16.0.0"
    implementation "com.google.firebase:firebase-perf:16.0.0"
    implementation "com.google.firebase:firebase-storage:16.0.1"

    // Analytics
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlytics_version@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:crashlytics-ndk:$crashlytics_ndk_version@aar") {
        transitive = true
    }

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    // Network - Gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // Reactive
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"

    // Simprints
    implementation "com.simprints:libscanner:2018.2.1"
    implementation 'com.simprints:libmatcher:1.3.2'

    // Test

    // TODO: migrate to JUnit 5. Tried https://github.com/mannodermaus/android-junit5 on 03/12/17 and it did not work out of the box.
    testImplementation("junit:junit:$junit_version") {
        exclude group: "com.android.support"
    }
    testImplementation 'org.robolectric:robolectric:4.0-alpha-3-SNAPSHOT'
    testImplementation 'org.robolectric:shadows-multidex:3.7.1'
    testImplementation 'com.android.support.test:monitor:1.0.2'

    testImplementation "com.google.truth:truth:0.42"
    androidTestImplementation("com.google.truth:truth:0.42"){
        exclude group: "com.google.guava"
        exclude group: "com.google.errorprone"
    }

    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'

    // 3.10 hangs around with roboeletric
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    androidTestImplementation 'com.nhaarman:mockito-kotlin:1.5.0'

    // Android test
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    androidTestImplementation('com.schibsted.spain:barista:2.5.0') {
        exclude group: "com.android.support"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.jetbrains.kotlin"
        exclude group: "com.google.guava"
    }

    androidTestImplementation "com.simprints:mockscanner:2018.3.0"
    testImplementation "com.simprints:mockscanner:2018.3.0"
    androidTestImplementation 'joda-time:joda-time:2.9.9'

    //UI
    implementation 'com.github.michael-rapp:android-material-dialog:4.3.3'
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:preference-v7:$support_version"
    implementation "com.android.support:support-fragment:$support_version"

    //Inject in tests
    testImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.4'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.4'
    testImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.4'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.4'
    androidTestImplementation 'com.jayway.awaitility:awaitility:1.6.3'

    //Permissions & Locations
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'pl.charmas.android:android-reactive-location2:2.1@aar'

}

def copyGoogleServicesJsonTask(variantName, gcpProject) {
    return tasks.create("copyGoogleServicesJson${variantName.capitalize()}", Copy) {
        def jsonName = "${gcpProject}-google-services.json".replaceAll("-", "_")
        from "${project.rootDir}/id/src/${variantName}/res/raw/${jsonName}"
        into "${project.rootDir}/id/src/${variantName}/"
        rename { filename -> "google-services.json" }
    }
}

copyGoogleServicesJsonTask("debug", getDevProjectId())
copyGoogleServicesJsonTask("release", "simprints-152315")
copyGoogleServicesJsonTask("releaseWithLogfile", "simprints-152315")
copyGoogleServicesJsonTask("endToEndTesting", "simprints-testing")

crashlytics {
    enableNdk true
    androidNdkOut "src/main/obj"
    androidNdkLibsOut "src/main/libs"
}

apply plugin: "com.google.gms.google-services"
