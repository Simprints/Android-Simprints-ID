apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "io.fabric"
apply plugin: "com.google.firebase.firebase-crash"
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: "realm-android"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 135
        versionName "2018.4.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        language {
            enableSplit = false
        }
    }
    
    buildTypes {
        release {
            lintOptions {
                // TODO : Remove whenever possible
                // Check in the Translations Editor for crucial translations that may be missing
                disable "MissingTranslation"
                abortOnError true
                fatal 'StopShip'
                checkReleaseBuilds true
            }
            minifyEnabled true
            ext.enableCrashlytics = true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            manifestPlaceholders = [firebase_performance_logcat_enabled: false, firebase_analytics_collection_enabled: true]
        }
        releaseWithLogfile {
            initWith release
        }
        debug {
            minifyEnabled false
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            versionNameSuffix "-dev"
            manifestPlaceholders = [firebase_performance_logcat_enabled: true, firebase_analytics_collection_enabled: true]
        }
    }
    
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release" ||
            variant.buildType.name == "releaseWithLogfile") {

            buildConfigField("String", "GCP_PROJECT", "\"simprints-152315\"")
            buildConfigField("String", "ANDROID_AUTH_API_KEY", "\"AIzaSyDdGdHCjfLY6z3zZSBrSHvjvNjh6K6Dhmw\"")
            buildConfigField("String", "END_POINT", "\"prod\"")
        } else if (variant.buildType.name == "debug") {
            buildConfigField("String", "GCP_PROJECT", "\"${getDevProjectId()}\"")
            buildConfigField("String", "ANDROID_AUTH_API_KEY", "\"AIzaSyCxPFr61OtOyAsVoXKSSSBo-C4Jk1QHc7I\"")
            buildConfigField("String", "END_POINT", "\"dev\"")
        }
    }

    testBuildType "debug"

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
}

def getDevProjectId() {
    if (project.hasProperty("development_gcp_project")) {
        return development_gcp_project
    } else {
        return "simprints-dev"
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://maven.fabric.io/public" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven {
        url "http://178.62.61.39:8081/artifactory/libs-release-local"
        credentials {
            username = "${SIMPRINTS_ARTIFACTORY_USERNAME}"
            password = "${SIMPRINTS_ARTIFACTORY_PASSWORD}"
        }
    }
}

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':libcommon')
    implementation project(':libsimprints')

    // Dagger
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    compileOnly "org.glassfish:javax.annotation:10.0-b28"

    // Kotlin awesomeness
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.anko:anko:0.10.8"
    implementation 'androidx.core:core-ktx:1.0.1'

    // Support libraries
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "com.google.android.material:material:1.1.0-alpha02"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // WorkManager
    implementation("android.arch.work:work-runtime-ktx:$work_version") {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    // Google
    implementation "com.google.android.gms:play-services-location:$google_version"
    implementation "com.google.android.gms:play-services-places:$google_version"
    implementation "com.google.android.gms:play-services-safetynet:$google_version"

    // Databases
    implementation "com.google.firebase:firebase-database:16.0.5"
    implementation "com.google.firebase:firebase-firestore:17.1.5"

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.6"
    implementation "com.google.firebase:firebase-auth:16.1.0"
    implementation "com.google.firebase:firebase-config:16.1.3"
    implementation "com.google.firebase:firebase-perf:16.2.3"
    implementation "com.google.firebase:firebase-storage:16.0.5"

    // Analytics
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.5@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:crashlytics-ndk:1.1.4@aar") {
        transitive = true
    }

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    // Network - Gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // Reactive
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxjava:2.1.16"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    // Simprints
    implementation "com.simprints:libscanner:2018.4.0"
    implementation 'com.simprints:libmatcher:1.3.2'

    // Test

    // TODO: migrate to JUnit 5. Tried https://github.com/mannodermaus/android-junit5 on 03/12/17 and it did not work out of the box.
    testImplementation("junit:junit:$junit_version") {
        exclude group: "com.android.support"
    }
    testImplementation "org.robolectric:robolectric:4.0.2"
    testImplementation "org.robolectric:multidex:3.4.2"
    testImplementation "org.robolectric:robolectric:4.0.2"

    testImplementation 'androidx.test:monitor:1.1.1'
    testImplementation "androidx.test.ext:junit:1.1.0"
    testImplementation "androidx.test:core:$androidx_version"
    testImplementation 'android.arch.core:core-testing:1.1.1'

    implementation "org.checkerframework:checker-compat-qual:2.5.3"
    testImplementation "com.google.truth:truth:0.42"
    androidTestImplementation("com.google.truth:truth:0.42"){
        exclude group: "com.google.guava"
        exclude group: "com.google.errorprone"
    }

    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'

    // 3.10 hangs around with roboeletric
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    androidTestImplementation 'com.nhaarman:mockito-kotlin:1.5.0'

    // Android test - https://developer.android.com/training/testing/release-notes
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation 'androidx.test:monitor:1.1.1'
    androidTestImplementation "androidx.test:core:1.1.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    testImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'


    androidTestImplementation('com.schibsted.spain:barista:2.5.0') {
        exclude group: "com.android.support"
        exclude group: "com.google.code.findbugs"
        exclude group: "org.jetbrains.kotlin"
        exclude group: "com.google.guava"
    }

    androidTestImplementation "com.simprints:mockscanner:2018.3.0"
    testImplementation "com.simprints:mockscanner:2018.3.0"
    androidTestImplementation 'joda-time:joda-time:2.9.9'

    //UI
    implementation 'com.github.michael-rapp:android-material-dialog:4.3.3'
    implementation "androidx.cardview:cardview:$androidx_version"
    implementation "androidx.preference:preference:$androidx_version"
    implementation "androidx.fragment:fragment:$androidx_version"

    //Inject in tests
    testImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.4'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.4'
    testImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.4'
    androidTestImplementation 'com.github.fabioCollini.daggermock:daggermock-kotlin:0.8.4'

    //Permissions & Locations
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'pl.charmas.android:android-reactive-location2:2.1@aar'

    //Room
    testImplementation "androidx.room:room-testing:$room_version"

    //WorkManager
    androidTestImplementation "android.arch.work:work-testing:$work_version"
}

def copyGoogleServicesJsonTask(variantName, gcpProject) {
    return tasks.create("copyGoogleServicesJson${variantName.capitalize()}", Copy) {
        def jsonName = "${gcpProject}-google-services.json".replaceAll("-", "_")
        from "${project.rootDir}/id/src/${variantName}/res/raw/${jsonName}"
        into "${project.rootDir}/id/src/${variantName}/"
        rename { filename -> "google-services.json" }
    }
}

copyGoogleServicesJsonTask("debug", getDevProjectId())
copyGoogleServicesJsonTask("release", "simprints-152315")
copyGoogleServicesJsonTask("releaseWithLogfile", "simprints-152315")
copyGoogleServicesJsonTask("endToEndTesting", "simprints-testing")

crashlytics {
    enableNdk true
    androidNdkOut "src/main/obj"
    androidNdkLibsOut "src/main/libs"
}

apply plugin: "com.google.gms.google-services"
