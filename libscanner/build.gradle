apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

project.version = "2018.2.1"

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName project.version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releaseWithLogfile {
            initWith release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
}

def libraryGroupId = 'com.simprints'
def libraryArtifactId = 'libscanner'

publishing.publications {
    aar(MavenPublication) {
        groupId libraryGroupId
        //noinspection GroovyAssignabilityCheck
        version project.version
        artifactId libraryArtifactId

        artifact("$buildDir/outputs/aar/${artifactId}-release.aar")
    }
}

artifactory {
    contextUrl = 'http://178.62.61.39:8081/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = SIMPRINTS_ARTIFACTORY_USERNAME
            password = SIMPRINTS_ARTIFACTORY_PASSWORD
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}
repositories {
    mavenCentral()
}
